# Maintainer: XZS <d dot f dot fischer at web dot de>

pkgname="gnome-shell-extension-scroll-workspaces-git"
pkgdesc="Switch workspaces by scrolling in the top panel"
pkgver=32.bd97630
pkgrel=1
arch=(any)
url="https://github.com/dfischer/gnome-shell-scroll-workspaces/"
license=(GPLv2)
depends=('gnome-shell>=3.8')
makedepends=('git')
install=gschemas.install
source=("$pkgname::git+$url#branch=conveniencejs")
md5sums=('SKIP')

pkgver() {
  cd $pkgname
  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package () {
  cd $pkgname
  extdir="$(find -name '*@*.*')"
  destdir="$pkgdir/usr/share/gnome-shell/extensions/$(basename $extdir)"
  schemadir="$destdir/schemas"
  install -d "$destdir" "$schemadir"

  cd "$extdir"
  install -m644 *.js* "$destdir"
  install -m644 schemas/*.xml "$schemadir"
}
