# Maintainer: XZS <d dot f dot fischer at web dot de>

pkgname='gnome-shell-extension-hidetopbar-git'
_dirname=hidetopbar
pkgdesc="Gnome 3 extension to hide the top bar except in overview mode"
pkgver=118.a7a66ff
pkgrel=2
arch=(any)
url='https://github.com/mlutfy/hidetopbar'
license=(GPLv2)
depends=('gnome-shell>=3.8')
install=gschemas-and-warn.install
source=('git+https://github.com/mlutfy/hidetopbar')
md5sums=('SKIP')

pkgver() {
  cd hidetopbar
  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

prepare() {
  # gnome-shell 3.16 is not officially supported.
  cd "$_dirname"
  sed -i '2s/]/, "3.16"]/' metadata.json
}

package() {
  cd "$_dirname"
  destdir="$pkgdir/usr/share/gnome-shell/extensions/hidetopbar@mathieu.bidon.ca"
  schemadir="$pkgdir/usr/share/glib-2.0/schemas"
  install -d "$destdir" "$schemadir"
  rm README.md
  mv *.xml "$schemadir"
  mv * "$destdir"
}
